#############################################################################################################################
#
# Author:   Thomas Fischer (xdajog) <mail@se-di.de>
# Desc:		This code is part of sediROM (http://forum.xda-developers.com/showthread.php?t=2789727)
#
# License:  This code is licensed under the Creative Commons License CC BY-SA 4.0.
#
# DISCLAIMER:
# The following deed highlights only some of the key features and terms of the actual license.
# It is NOT a license and has NO legal value. You should carefully review ALL of the terms and conditions of the actual
# license before using the licensed material.
#
# Please check the following link for details and the full legal content:
# http://creativecommons.org/licenses/by-sa/4.0/legalcode
#
#    You are free to:
#
#       Share — copy and redistribute the material in any medium or format
#       Adapt — remix, transform, and build upon the material
#
#       for any purpose, even commercially.
#       The licensor cannot revoke these freedoms as long as you follow the license terms.
#
#    Under the following terms:
#
#       Attribution:
#          You must give appropriate credit, provide a link to the license, and indicate if changes were made.
#          You may do so in any reasonable manner, but not in any way that suggests the licensor endorses you or your use.
#
#       ShareAlike:
#          If you remix, transform, or build upon the material, you must distribute your contributions under the same
#          license as the original.
#
#############################################################################################################################
#
### FILE INFO:
#
# AROMA Installer 2.70 - AROMA Installer Configuration
#       (c) 2011-2012 by Ahmad Amarullah
#           amarullz - xda-developers
#           http://www.amarullz.com/
#
#       Binary Version  : 2.70B
#       Binary Codename : Flamboyan
#
### INFO:
#
# * AROMA Resource Dir  = META-INF/com/google/android/aroma
#
# * AROMA Temporary Dir = /tmp/aroma/
#
# * About Icon          = @alert - show <AROMA Resource Dir>/icons/alert.png
#                         or if theme used it will find <themedir>/icon.alert.png
#                         you can still access it via "icons/alert"
#
# * dp                  = Device Pixelate. WVGA/QHD = 3, HVGA = 2.
#                         dp calculated with = round(min(width,height) / 160)
#
###
#
#
# Aroma Guides:
# http://forum.xda-developers.com/showthread.php?t=1461712
# http://forum.xda-developers.com/showthread.php?t=2422457
#
########################################################################

calibrate("0.8429","51","0.9658", "9", "yes");
ini_set("rom_name",             "sediROM");

# workaround START because ini_get is buggy in Aroma #
ini_set("rom_date",             "2015-06-16");
writetmpfile(
	   "build.repl",
					  "buildversion=v2.0.63"
);
#so to have both the getpropfile and the ini setting I use here ini_set, too:
ini_set("rom_version",			file_getprop("/tmp/aroma/build.repl","buildversion"));
# workaround END #

ini_set("rom_author",           "xdajog");
ini_set("rom_device",           "Samsung Captivate Glide / SGH-I927");

anisplash(
    4,
    "splash/a1", 500,
    "splash/a2", 30,
    "splash/a3", 30,
    "splash/a4", 30,
    "splash/a5", 30,
    "splash/a6", 30
);

theme("miui");
loadlang("langs/en.lang");
fontresload( "0", "ttf/Roboto-Regular.ttf", "12" );
fontresload( "1", "ttf/Roboto-Regular.ttf", "14" );

# disable vibration (will work only in AROMA v2.70)
#ini_set("vibrate", "0");

viewbox(
    "Welcome",
    "You are about to install/modify\n\n<b><#selectbg_g>"+
	  ini_get("rom_name")+
	"</#></b>\n\nfor \n\n<b>"+ini_get("rom_device")+"</b>.\n\n"+
    "Relevant information of the ROM:\n\n"+
    "  AUTHOR\t: <b><#selectbg_g>"+ini_get("rom_author")+"</#></b>\n"+	
    "  VERSION\t: <b><#selectbg_g>"+ini_get("rom_version")+"</#></b>\n"+
    "  UPDATED\t: <b><#selectbg_g>"+ini_get("rom_date")+"</#></b>\n\n\n"+
    "DO BOTH (the installer can NOT do that for you): \n\n"+
    "1) NANDROID backup\n   (do NOT skip that step!)\n"+
    "2) AND efs backup \n\n"+
    "and do that BEFORE FLASHING and also when upgrading only!\n\n\n"+
    "Press Next to continue the installation...",
    "@info"
);


textbox(
  #-- Title
    "CHANGELOG",
  
  #-- Subtitle
    "sediROM changelog",
  
  #-- Icon
    "@update",
  
  #-- file
    zipread("tmp/CHANGES")
);


menubox(
    "Installation Mode",
    "Please select installation method for sediROM Installer",
    "@install",
    "type.prop",

    "Easy Installation/Upgrade",   "Install or upgrade sediROM.","@agreement",
	"Modify Installation",    "Modify an existing installation. Removing features are not supported: for that you need to wipe/reset or do it manually.", "@personalize"
);

###################################################################################################################
#                                                                                                                 #
#                     			AROMA INSTALLER for sediROM		                                      			  #
###################################################################################################################

# this will prepare to automatically update the build.prop with version number...so damn cool :o)
# the effective update process will then be done at the end of the updater-script

setvar("SED","s/ro.build.display.id=.*/ro.build.display.id=sediROM_");
appendvar("SED", file_getprop("/tmp/aroma/build.repl","buildversion"));
appendvar("SED","/g");
ziptotmp("customize/post/build.prop", "build.prop");
run_program("/sbin/busybox", "sed", "-i", getvar("SED"), "/tmp/aroma-data/build.prop");


####################################################################################################################
##########  						MODE: NORMAL/EASY Installation/Upgrade									############
####################################################################################################################

if prop("type.prop","selected") == "1" then

	writetmpfile(
		  "systemcore.prop",
			"item.1.1=1\n"
	);
	writetmpfile(
		  "fixpermissions.prop",
			"item.1.1=1\n"
	);

	selectbox(
				"Data Wipe?",
				"Please select desired wipe option.",	  
				"@default",	  
				"wipe.prop",	  

					"Data Wipe?",             				"",2,                                 						#-- Group 1. key = "selected.1"
					"Clean install / Recommended Wipe",		"For the PERFECT sediROM experience.",1,					#-- selected.1 = 1
					"Upgrade mode / Partial wipe",  		"For upgrading sediROM only! Leaves user apps, settings, data.",0  	#-- selected.1 = 2

		);

	writetmpfile(
		  "mods.prop",
			"selected.1=4\n"+
			"selected.2=1\n"+
			"selected.3=2\n"+
			"selected.4=2\n"
	);

	writetmpfile(
		  "features.prop",
			"item.1.1=0\n"+			
			"selected.1=2\n"+
			"selected.2=1\n"+
			"selected.3=3\n"+
			"item.3.1=1\n"+
			"selected.4=1\n"+
			"selected.5=1\n"+
		    "selected.7=0\n"
	);

	writetmpfile(
		  "apps.prop",
			"item.1.1=1\n"+
			"item.1.2=1\n"+
			"item.1.3=1\n"+
			"item.1.4=1\n"+
			"item.1.5=1\n"+
			"item.1.6=1\n"+
			"item.1.7=1\n"+
			"item.1.8=0\n"+
			"item.1.9=0\n"+
			"item.1.10=1\n"+
			"item.1.11=1\n"+
			"item.1.12=1\n"+
			"item.1.13=0\n"+
			"item.1.14=0\n"+
			"item.1.15=0\n"+
			"item.1.16=1\n"+
			"item.1.17=0\n"+
			"item.1.18=0\n"+
			"item.1.19=0\n"
	);

	writetmpfile(
		  "addapps.prop",
			"item.1.1=1\n"+
			"item.1.2=0\n"+
			"item.1.3=0\n"+
			"item.1.4=1\n"+
			"item.1.5=0\n"+
			"item.1.6=0\n"+
			"item.1.7=1\n"+
			"item.1.8=1\n"+
			"item.1.9=1\n"

	);

	selectbox(
				"Enterprise Mode",
				"Please decide if you want to have sediROM rooted or not. To have sediROM un-rooted can be a requirement to run Enterprise Apps.",
				"@default",
				"root.prop",
					
					"Root option",     "Please decide if you want to have sediROM rooted or not. To have sediROM un-rooted can be a requirement to run Enterprise Apps.",2,
					"Rooted (Recommended)",      "Recommended/Required to run free root-apps like Titanium Backup etc.  This is the recommended mode when you do not need to fit a companies security policy or running special Enterprise apps which require an unrooted ROM.",1,	#selected.1
					"UN-Rooted (not fully tested yet)",     "Enterprise Apps and Users may need this mode in order to run or if you want to fit your companies security policy.",0      #selected.1
					
		);

		
	checkbox(
				"GPS Options",
				"Please select apps you want to include or exclude below.",
				"@default",
				"gps.prop",
		 
					
					"GPS Options",     "If you are Canadian and are having trouble getting a fix, try the Rogers RUXKJ5 Modem (choose modify in installer)",2,
					"GPS Tweaks",      "GPS Tweaks",1,								#item.1.1
					"GPS Regions",     "Select a Region for faster GPS lock",1      #item.1.2 
					
		);

	if prop("gps.prop","item.1.2") == "1" then

	selectbox(
				"Select Region",
				"Please select your region...",	  
				"@default",	  
				"gpsregion.prop",	  

					"Region for GPS",       "",2,           #-- Group 1. key = "selected.1"
					"Africa",          		"",0,			#-- selected.1 = 1
					"Asia",          		"",0,			#-- selected.1 = 2
					"Europe",               "",1,			#-- selected.1 = 3
					"North America",        "",0,			#-- selected.1 = 4
					"Oceania",   			"",0,			#-- selected.1 = 5
					"South America",   		"",0,			#-- selected.1 = 6
					"UK",   				"",0,			#-- selected.1 = 7
					"US Mid-East",   		"",0			#-- selected.1 = 8

		);
	endif;	

	if prop("gps.prop","item.1.2") == "1" then	
	writetmpfile(
		  "gpsregion.prop",
			"selected.1=9\n"
	);
	endif;	

	checkbox(
				"Utils",
				"Everytime while in a call Noise Reduction will be enabled. With this option if you disable NR in a call the next time it will start without NR! You can enable it at anytime again - when enabled at the next call it will start with NR enabled.",
				"@default",
				"utils.prop",
					
					"Phone",     			"Phone mods",														2,		# Group 1
					"Noise Reduction Mod",  "If you want to remember your last noise reduction choice in a phone call (can fix echo's).",	0      	# item.1.1 
		);

	writetmpfile(
		  "tweaks.prop",
			"item.1.1=1\n"+
			"item.1.2=0\n"+
			"item.1.3=0\n"+
			"item.1.4=0\n"+
			"item.1.5=1\n"+
			"item.1.6=1\n"+
			"item.1.7=0\n"+
			"item.1.8=0\n"+
			"item.1.9=0\n"		
	);
		
	selectbox(
				"Select Bootanimation",
				"Please select desired Bootanimation...",	  
				"@default",	  
				"bootanimation.prop",	  

					"Bootanimations",                   "",2,           #-- Group 1. key = "selected.1"
					"LiteROM Bootanimation",          	"",0,		#-- selected.1 = 1
					"Android Minimal",                  "",0,			#-- selected.1 = 2
					"Android White Particles",          "A nice looking Android animation",0,			#-- selected.1 = 3
					"Win7Nexus",   						"",0,			#-- selected.1 = 4
					"Android Green Particles",          "An amazing green boot logo",0,			#-- selected.1 = 5
					"AOSP Default",   					"",0,			#-- selected.1 = 6
		);
		

	writetmpfile(
		  "audio.prop",
			"item.1.1=0\n"+
			"item.1.2=0\n"		
	);
	
endif;
####################################################################################################################
##########  						END - NORMAL Installation/Upgrade - END							############
####################################################################################################################

####################################################################################################################
##########  							MODE: Modifying Jump Station 									############
####################################################################################################################
#####
# Entrypoint for Modify Menu
# Usage: goto("aroma_installer_modmenu_pos");
gotolabel("aroma_installer_modmenu_pos");

# check if we're coming from a finished installer:
if getvar("goto_modmenu")=="1"
then
	   viewbox(
		   "Modify mode started",
		   "\n\nThe wizard is ready to begin customization.\n\n"+
		   "Press next to continue.\n\n"+
		   "Press the Menu Key -> Quit Installation if you want to exit the wizard.",
		   "@info"
	   );
   	   ini_set("text_next", "Next");

	   #####
	   # clean up first:
	   run_program("/sbin/busybox", "rm",
	   	   "/tmp/aroma/root.prop",
		   "/tmp/aroma/wipe.prop",
		   "/tmp/aroma/systemcore.prop",
		   "/tmp/aroma/btfix.prop",
		   "/tmp/aroma/fixpermissions.prop",
		   "/tmp/aroma/apps.prop",
		   "/tmp/aroma/addapps.prop",
		   "/tmp/aroma/mods.prop",
		   "/tmp/aroma/radio.prop",
		   "/tmp/aroma/gps.prop",
		   "/tmp/aroma/gpsregion.prop",
		   "/tmp/aroma/utils.prop",
		   "/tmp/aroma/features.prop",
		   "/tmp/aroma/bootanimation.prop",
		   "/tmp/aroma/tweaks.prop",
		   "/tmp/aroma/audio.prop");
	   
	   # preselect the modify type
       writetmpfile(
		  "type.prop",
			"selected=2\n"
	   );
endif;
####################################################################################################################
##########  							MODE: Modifying Jump Station - END	 							############
####################################################################################################################

####################################################################################################################
##########  							MODE: Modifying Installation 									############
####################################################################################################################
if prop("type.prop","selected") == "2"
then

	writetmpfile(
		  "wipe.prop",
			"selected.1=3\n"
	);
	writetmpfile(
		  "systemcore.prop",
			"item.1.1=0\n"
	);
	writetmpfile(
		  "fixpermissions.prop",
			"item.1.1=0\n"
	);

	writetmpfile(
		  "apps.prop",
			"item.1.1=0\n"+
			"item.1.2=0\n"+
			"item.1.3=0\n"+
			"item.1.4=0\n"+
			"item.1.5=0\n"+
			"item.1.6=0\n"+
			"item.1.7=0\n"+
			"item.1.8=0\n"+
			"item.1.9=0\n"+
			"item.1.10=0\n"+
			"item.1.11=0\n"+
			"item.1.12=0\n"+
			"item.1.13=0\n"+
			"item.1.14=0\n"+
			"item.1.15=0\n"+
			"item.1.16=0\n"+
			"item.1.17=0\n"+
			"item.1.18=0\n"+
			"item.1.19=0\n"
	);

	writetmpfile(
		  "addapps.prop",
			"item.1.1=0\n"+
			"item.1.2=0\n"+
			"item.1.3=0\n"+
			"item.1.4=0\n"+
			"item.1.5=0\n"+
			"item.1.6=0\n"+
			"item.1.7=0\n"+
			"item.1.8=0\n"+
			"item.1.9=0\n"
	);

	selectbox(
				"Enterprise Mode",
				"Please decide if you want to have sediROM rooted or not. To have sediROM un-rooted can be a requirement to run Enterprise Apps.",
				"@default",
				"root.prop",
					
					"Root option",     "Please decide if you want to have sediROM rooted or not. To have sediROM un-rooted can be a requirement to run Enterprise Apps.",2,
					"Rooted (Recommended)",      "Recommended/Required to run free root-apps like Titanium Backup etc.  This is the recommended mode when you do not need to fit a companies security policy or running special Enterprise apps which require an unrooted ROM.",1,	#selected.1
					"UN-Rooted (not fully tested yet)",     "Enterprise Apps and Users may need this mode in order to run or if you want to fit your companies security policy.",0      #selected.1
					
		);

	selectbox(
				"ROM Features",
				"Please select features you wish to add",	  
				"@default",	  
				"mods.prop",	  

					"Kernel area (grouped by their source)",  "The kernel area",  																	2,  #-- Group 1. key = "selected.1"
					"Stock-UCLJ3_xdajog",  "Latest Samsung Stock Kernel but with Bluetooth fix and custom bootanimation (v3.1.10)", 				0,  #-- selected.1 = 1
					"Stock-UCLJ3",  "Bluetooth will loose pairings every reboot! Untouched & pure Samsung Stock Kernel (v3.1.10)",					0,  #-- selected.1 = 2
		 			"Stock-UCLH2",  "Bluetooth will loose pairings every reboot! Untouched & pure Samsung Stock Kernel",  							0,  #-- selected.1 = 3
					"sediKERNEL_v1.0",  "xdajog's kernel (v3.1.10). L2_hsic patched. Based on CM11 by bubor. MTP working fine!",					0,  #-- selected.1 = 4
					"CM11_bubor",  "Bubor's CM11 kernel (v3.1.10). L2_hsic patched. MTP IS NOT working!!!",											0,  #-- selected.1 = 5
	                "LK_r20150508_bubor",  "LiteKernel L2_hsic patched. Made by bubor at 2015-05-08 with Linaro 4.9.1.",							0,  #-- selected.1 = 6
	                "LK_r20130222_xdajog",  "4.2 Litekernel by thegreatergood migrated by xdajog. MTP IS NOT working!!!", 							0,  #-- selected.1 = 7					
	                "LK_r20130221_xdajog",  "4.2 Litekernel by thegreatergood migrated by xdajog. Good battery life.", 								0,  #-- selected.1 = 8
	                "LK_v1.2.2_UV-noOC",  "Old LiteKernel not OverClocked and with UnderVoltage.",  												0,  #-- selected.1 = 9
	                "LK_v1.2.2_UV-gpuOC",  "Old LiteKernel v1.2.2 GPU OverClocked and with UnderVoltage",  											0,  #-- selected.1 = 10     
					"LK_v1.2.2_LR-0.9",  "Original old LiteRom-v0.9 Kernel.Tweaked - LiteKernel overclocked with UnderVoltage",  					0,  #-- selected.1 = 11


					"Modem/Radio/Baseband",     "Choose another Modem if you want",             			2,  #-- Group 2. key = "selected.2"
					"UCLJ3 Modem",         		"Latest available AT&T Modem",                          	0,  #-- selected.2 = 1
					"UCKL1 Modem",  	   		"AT&T UCKL1 Modem",              							0,  #-- selected.2 = 2
					"UCLH2 Modem",  	   		"AT&T UCLH2 Modem",              							0,  #-- selected.2 = 3
					"UCKI3 Modem",  	   		"AT&T UCKI3 Modem",              							0,  #-- selected.2 = 4
					"RUXKJ5 Modem",  	   		"Rogers RUXKJ5 Modem",              						0,  #-- selected.2 = 5

					"BLN Support",       "",                                                                                             			2,  #-- Group 3. key = "selected.3"
					"Stock",             "",                                                                      						 			0,  #-- selected.3 = 1
					"Enable BLN",        "Enable Backlight support - you need a BLN app to use this!",                          					1,  #-- selected.3 = 2				
					
					"Sim Unlock",        "",                                                                                             			2,  #-- Group 5. key = "selected.5"
					"Stock",             			  "Recommended for users with Factory Unlock",                      						 	0,  #-- selected.5 = 1
					"Disable ICS Security Check",     "Disables Security Check in Rom, allows you to use modified nv_data.bin",                     1   #-- selected.5 = 2     
		);

	if ((prop("mods.prop","selected.1") != "0") && (prop("mods.prop","selected.2") == "")) then
		selectbox(
					"Radio Options",
					"You have choosen to (re)install a Kernel without a modem ("+prop("mods.prop","selected.2")+","+prop("mods.prop","selected.1")+") In order to get that fully work you need to reinstall a modem,too. Please choose one. ",
					"@default",
					"radio.prop",

						"Modem",        "I force to re-install because otherwise after a Kernel update you maybe won't get baseband...",                           2,  #-- Group 2. key = "selected.2"
						"UCLJ3 Modem",         "Latest available AT&T Modem",                                                            1,  #-- selected.2 = 1
						"UCKL1 Modem",  	   "AT&T UCKL1 Modem",              														 0,  #-- selected.2 = 2
						"UCLH2 Modem",  	   "AT&T UCLH2 Modem",              														 0,  #-- selected.2 = 3
						"UCKI3 Modem",  	   "AT&T UCKI3 Modem",              														 0,  #-- selected.2 = 4
						"RUXKJ5 Modem",  	   "Rogers RUXKJ5 Modem",              														 0  #-- selected.2 = 5
			);
	endif;

	checkbox(
				"GPS Options",
				"Please choose if you want to tune GPS:",
				"@default",
				"gps.prop",
		 
					
					"GPS Options",     "If you are Canadian and are having trouble getting a fix, try the Rogers RUXKJ5 Modem",2,
					"GPS Tweaks",      "GPS Tweaks",0,								#item.1.1
					"GPS Regions",     "Select a Region for faster GPS lock",0      #item.1.2 
					
		);

	if prop("gps.prop","item.1.2") == "1" then

	selectbox(
				"Select Region",
				"Please select your region...",	  
				"@default",	  
				"gpsregion.prop",	  

					"Region for GPS",       "",2,           #-- Group 1. key = "selected.1"
					"Africa",          		"",0,			#-- selected.1 = 1
					"Asia",          		"",0,			#-- selected.1 = 2
					"Europe",               "",1,			#-- selected.1 = 3
					"North America",        "",0,			#-- selected.1 = 4
					"Oceania",   			"",0,			#-- selected.1 = 5
					"South America",   		"",0,			#-- selected.1 = 6
					"UK",   				"",0,			#-- selected.1 = 7
					"US Mid-East",   		"",0			#-- selected.1 = 8

		);

	endif;	

	checkbox(
				"Utils",
				"Please select apps/functions you want to include or exclude below.",
				"@default",
				"utils.prop",
					
					"Phone",     			"Phone mods",														2,		# Group 1
					"Noise reduction Mod",  "If you want to disable noise reduction by default to fix echos.",	0      	# item.1.1 
		);

	checkbox(
				"Main Features",
				"Please select desired features to be ADDED. This will not CHANGE/REMOVE existing ones.",	  
				"@default",	  
				"features.prop",	  

					"Camera",                    "",                                        	2, #-- Group 1. key = "selected.1"
					"HD Camera ULTRA",         	"A stable photo app.",    						0, #-- selected.1 = 1
					"Open Camera",              "Most stable photo+video app in my case.",    	0, #-- selected.1 = 2
					"Stock Camera",             "Gallery Button Doesn't work!",        			0, #-- selected.1 = 3
					"ICS Camera",         		"May crash sometimes",             				0, #-- selected.1 = 4				
					"Android 4.2 Camera",       "May crash sometimes", 							0, #-- selected.1 = 5

					"Launcher",				        	"",	                               	2, #-- Group 2. key = "selected.2"
					"ATOM Launcher",      				"An up-to-date version of the ATOM launcher. IMO it's one of the best and very stable.",                    0, #-- selected.2 = 1	
					"Nova Launcher",		 			"(working but old version)",        				         0, #-- selected.2 = 2 
					"Apex Launcher",      				"(working but old version)",                                 0, #-- selected.2 = 3
					"Stock Launcher",      				"(working but old version)",                                 0, #-- selected.2 = 4
					"ADW Launcher",      				"(working but old version)",                                 0, #-- selected.2 = 5	

					"Keyboard",          "",                                                2, #-- Group 3. key = "selected.3"
					"Kii Keyboard (DON'T WORK!)",  "Very good keyboard. you can enable swipe in settings, too.",  					0, #-- selected.3 = 1		
					"Stock Keyboard",    "",                                                0, #-- selected.3 = 2
					"Samsung Keyboard",  "",                          						0, #-- selected.3 = 3
					"Go Keyboard",  "",                          						    0, #-- selected.3 = 4
					
					"SMS App",          "",                                                 2, #-- Group 4. key = "selected.4"
					"LiteROM MMS",      "A very simple but effective app. Simple read and write SMS/MMS.",          0, #-- selected.4 = 1
					"GoSMS",			"",                          						0, #-- selected.4 = 2
					"Handscent",        "",                          						0, #-- selected.4 = 3
					
					
					"Themes",           "",                                                 2, #-- Group 5. key = "selected.5"
					"LiteROM Theme",    "",                                                 1, #-- selected.5 = 1
					"Stock Theme",		"",                          						0, #-- selected.5 = 2

					"Bluetooth",		"",	                               					2, #-- Group 6. key = "selected.6"
					"Original Bluetooth",		 "Install/Reinstall Bluetooth stack. Use it when you have problems only.",      0, #-- selected.6 = 1 

					"Tools",		"",	                               						2, #-- Group 7. key = "selected.7"
					"Connectbot",		 "Install/Reinstall Connectbot terminal app.",      0, #-- selected.7 = 1
					"Google Chrome",	 "Install/Reinstall Google Chrome Browser.",      	0  #-- selected.7 = 2  
		);


	selectbox(
				"Select Bootanimation",
				"Please select desired Bootanimation... ",	  
				"@default",	  
				"bootanimation.prop",	  

					"Bootanimations",                   "",2,           #-- Group 1. key = "selected.1"
					"LiteROM Bootanimation",          	"",0,			#-- selected.1 = 1
					"Android Minimal",                  "",0,			#-- selected.1 = 2
					"Android White Particles",          "",1,			#-- selected.1 = 3
					"Win7Nexus",   						"",0,			#-- selected.1 = 4
					"Android Green Particles",          "",0,			#-- selected.1 = 5
					"AOSP Default",   				"",0			#-- selected.1 = 6

		);
		
	checkbox(
				"Select Tweaks",
				"Please select desired tweaks...",	  
				"@default",	  
				"tweaks.prop",	  

					"Custom Tweaks",                   "",2,
					"Bluetooth HSP Fix",  "This (re-)enables the use of shutdown scripts and installs the BT HSP workaround.",0,			  #item.1.1				
					"LiteROM Tweaks",  "Currently (re-testing): Custom LiteROM Tweaks for better battery life and general perfomance",0,							#item.1.2
					"V6 SuperCharger",  "Currently (re-testing): Famous script by zeppelinrox, meant to improve all around device performance",0,			#item.1.3
					"KickAssKernelizer",  "Currently (re-testing): Famous script by zeppelinrox, Includes various Kernel tweaks",0,						    #item.1.4
					"Seeder Entropy Generator",  "Recommended(DD): An Android Mod/Patch made by lambgx02 which significantly reduces lag",0,					#item.1.5
					"Increase Bluetooth Audio Quality",  "Recommended(DD): Mod made by lambgx02 to improve bluethooth audio Quality",0,					    	#item.1.6
					"Virtuous OC Daemon",  "Reduces CPU Frequency as battery runs out to increase battery life. Better do NOT use it if you want to use other tuning apps later..",0,				#item.1.7
					"Swap Internal and External Sdcard Mounts",  "Use your large External Sdcard as your primary sdcard",0,				        #item.1.8
					"Netforks Tweaks",  "Should boost Internet Speed. BUT: In my case this mod made problems. It results in bad wifi signal and eats battery..",0			  #item.1.9

		);
		
	checkbox(
				"Select Audio Options",
				"Please select desired mods...",	  
				"@default",	  
				"audio.prop",	  

					"Audio Options",                   "",2,
					"Samsung Music Player",         "",0, 								            #item.1.1
					"Google Play Music ",           "",0 											#item.1.2
		);

endif;
####################################################################################################################
##########  					END		MODE: Mofifying Installation 									############
####################################################################################################################

# now a speciality for another f... edify bug
# well I begin to hate edify.. wildcards seems to not work (at least in the updater-script) so I need to do this stupid things
# when using e.g. chattr or ls with a wildcard it will fail so I need the full path which can be different for each device
if run_program("sbin/busybox", "ls", "-l", "/data") != 0 
then
	if run_program("/sbin/busybox", "mount", "-t", "auto", "/dev/block/mmcblk0p6", "/data") != "0"
	then
		writetmpfile(
			"btfix.log", 
			"ERROR mounting /data partition!\n"
		);
		abort();
	else
		writetmpfile(
			"btfix.log", 
			"/data partition successfully mounted manually..\n"
		);
	endif;
else
		writetmpfile(
			"btfix.log", 
			"/data partition already mounted..\n"
		);
endif;
if exec("/sbin/busybox", "grep", "-rils", "name", "/data/misc/bluetoothd/") == "0"
  	then
		writetmpfile(
			"btfix.log", 
			"Mounted /data successfully will start some prechecks...\n"+
			"Previous BT path detected: "+getvar("exec_buffer")+"\n"
		);
  		writetmpfile(
	  		"btfix.prop",
			"btfix=1\n"+
			"btpath="+getvar("exec_buffer")+
			"\n"
		);
	else
		writetmpfile(
			"btfix.log", 
			"Mounted /data successfully will start some prechecks...\n"+
			"No previous BT path detected: <"+getvar("exec_buffer")+"> (<-- should be empty)\n"
		);
  		writetmpfile(
	  		"btfix.prop",
			"btfix=0\n"+
			"btpath="+getvar("exec_buffer")+
			"\n"					
		);		
endif;

ini_set("text_next", "Install Now");

viewbox(
    "Ready to Install",
    "The wizard is ready to begin installation.\n\n"+
	"Press <b>Install Now</b> to begin the installation.\n\n"+
	"If you want to review or change any of your installation settings, press <b>Back</b>. Press Menu Key -> Quit Installation to exit the wizard.",
    "@confirm"
);

ini_set("text_next", "Next");

setvar("retstatus",
  install(
      "Installing",
	
	  "<b>"+ini_get("rom_name")+"</b> is being installed.\n\n"+
      "Please wait while installation wizard installs <b>"+ini_get("rom_name")+
	  "</b>. This may take several minutes.",
	
      "@install",
	
      "The <b>"+ini_get("rom_name")+"</b> installation wizard has ended."+
	  "Press Next to continue."
  )
);

# always write a log file to have it always there when needed..!! 
setvar("LPATH", "/sdcard/install_sediROM_");
setvar("LOG", file_getprop("/tmp/aroma/build.repl","buildversion"));
appendvar("LPATH",getvar("LOG"));
appendvar("LPATH",".log");
run_program("/sbin/busybox", "cp", "/tmp/aroma/btfix.log", getvar("LPATH")+"_btfix");
run_program("/sbin/busybox", "cp", "/tmp/aroma/.install.log", getvar("LPATH"));

ini_set("text_next", "I-will-do-so");

viewbox(
    "Please note",
    "IMPORTANT:\n\nYou HAVE TO reboot once AGAIN - right after the next boot and Welcome Wizard has finished!\n\n\n\n"+
    "In other words: boot twice before using sediROM.\n\n",
    "@confirm"
);

ini_set("text_next", "Next");

# When installation was a success: Modify installation wanted?
if getvar("retstatus") == "0"
then
		   checkviewbox(
			 #-- Title
			   "Modify Installation jump station",
			 
			 #-- Text
			   "\n\n\n\nInstallation has finished.\n\nDo you want to go directly to the \n\n<b>MODIFY screen/mode</b>?\n\n"+
			   "If you want to change settings in sediROM like kernel, modem etc you can do so right now by selecting this option.\n",
			   
			 #-- Icon
			   "@welcome",
		   
			 #-- Checkbox Text
			   "Start sediROM MODIFY mode",
		   
			 #-- Initial Checkbox value ( 0=unchecked, 1=checked ) -  (Optional, default:0)
			   "0",
		   
			 #-- Save checked value in variable "reboot_it" (Optional)
			   "goto_modmenu"
		   );
		   if getvar("goto_modmenu")=="1"
		   then
			 goto("aroma_installer_modmenu_pos");
		   endif;
endif;

# else show the reboot option
checkviewbox(
    "Installation Completed",
    "<#selectbg_g><b>Congratulation...</b></#>\n\n"+
    "<b>"+ini_get("rom_name")+"</b> has been installed into your device.\n\n"+
    "Installer Status: "+getvar("retstatus")+"\n\n",
    "@update",
    "Reboot your device now.",
    "1",
    "reboot_it"
);


if
  getvar("reboot_it")=="1"
then
  run_program("/sbin/busybox", "reboot");
endif;
